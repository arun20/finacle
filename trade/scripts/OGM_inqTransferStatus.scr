#============================================================
# IMPORTANT: Auto Generated Header. DO NOT CHANGE MANUALLY.
# Product: Finacle Core
# Category: User Defined
# Script: OGM_inqTransferStatus.scr
# Type: NA|default
# Description: OGM_inqTransferStatus.scr
# Author: Saisomasekhar_N
# Date: 6/22/18 3:18 PM
# Modification Log:
#============================================================
IMPORT LibslTranSeq
IMPORT LibCommonB001
IMPORT LibCommonB002
<--START
	# Script body goes here
	TRACE ON
	sub_createRepClassB("CUST","DATA")
	
	### 1.INITIALIZE VARIABLES ###
	sub_copyOptionalFieldB("errorFlg","CUST","DATA","errorFlg","N")
	sub_copyOptionalFieldB("errorMsg","CUST","DATA","errorMsg","")
	
	### 2.GET MENU ID ###
	sv_a = func_getCallingMenuOptionB("CMMSG00004",BYREF CUST.DATA.menuId)
	PRINT(sv_a)
	PRINT(CUST.DATA.menuId)
	IF((CUST.DATA.menuId != "OGM")AND(CUST.DATA.menuId != "ODCM")AND(CUST.DATA.menuId != "IGM")AND(CUST.DATA.menuId != "IDCM"))THEN
	#{
		EXITSCRIPT
	#}
	ENDIF
	
	### 3.GET SL TRAN SEQUENCE ###
	CUST.DATA.txnSeq = ""
	sv_a = func_getslTranSeq(BYREF CUST.DATA.txnSeq)
	PRINT(sv_a)
	IF(sv_a != 0)THEN
	#{
		CUST.DATA.errorFlg = "Y"
		CUST.DATA.errorMsg = "Generation of Transaction Sequence Failed."
		GOTO ENDOFSCRIPT
	#}
	ENDIF
	PRINT(CUST.DATA.txnSeq)
	
	CUST.DATA.chkTxnSeq = ""
	sv_a = func_getslTranSeq(BYREF CUST.DATA.chkTxnSeq)
	PRINT(sv_a)
	IF(sv_a != 0)THEN
	#{
		CUST.DATA.errorFlg = "Y"
		CUST.DATA.errorMsg = "Generation of Transaction Sequence Failed."
		GOTO ENDOFSCRIPT
	#}
	ENDIF
	PRINT(CUST.DATA.chkTxnSeq)
	
	### 4.INPUTS FOR API ###
	sub_copyOptionalFieldB("inpDate","CUST","DATA","inpDate",BANCS.STDIN.BODDate)
	BANCS.INPARAM.BINDVARS = LEFT$(BANCS.STDIN.BODDate,10)
	sv_a = "inpDate|SELECT TO_CHAR(TO_DATE(?SVAR,'DD-MM-YYYY'),'YYYY-MM-DD') FROM DUAL"
	sv_a = urhk_dbSelectWithBind(sv_a)
	sub_copyOutparamFieldB("inpDate","CUST","DATA","inpDate","inpDate")
	PRINT(CUST.DATA.inpDate)
	BANCS.INPUT.tellerId 	= BANCS.STDIN.userId
	BANCS.INPUT.txnSeq 		= CUST.DATA.txnSeq
	BANCS.INPUT.chkTxnDate 	= CUST.DATA.inpDate
	BANCS.INPUT.chkTxnSeq 	= CUST.DATA.chkTxnSeq
	CALLSCRIPTIFEXIST("InqTransferStatus.scr")
	IF(FIELDEXISTS(INTF.DATA.orgCode))THEN
	#{
		IF(INTF.DATA.orgCode != "000")THEN
		#{
			CUST.DATA.errorMsg = INTF.DATA.orgCode+"-"+INTF.DATA.ErrorDesc
			CUST.DATA.errorFlg = "Y"
			GOTO ENDOFSCRIPT
			#}
		ELSE
			#{
			### 5.CALL API FOR FUND TRANSFER IF SUCCESS ###
			CALLSCRIPTIFEXIST("OGM_inqTransferFund.scr")
			IF(CUST.DATA.errorFlg == "Y")THEN
			#{
				EXITSCRIPT
				#}
			ELSE
				#{
				### GET KEY FIELD VALUES FROM SRV - MENU SPECIFIC ###
				IF(CUST.DATA.menuId == "OGM")THEN
				#{
					CUST.DATA.srvFieldNames = "bgCritDtl.bgFuncCode|bgCritDtl.bgSrlNum|bgCommonDtl.bgB2kId|bgCritDtl.bgType.type|bgChargeDtl.entityId.entityType"
					CUST.DATA.srvFieldNames = CUST.DATA.srvFieldNames + "|" + "bgChargeDtl.eventSrlNum|bgCommonDtl.bgEventAmt|bgCritDtl.bgCritCrncy"
					CUST.DATA.srvFieldNames = CUST.DATA.srvFieldNames + "|" + "bgAmendDtl.amendInd|bgAmendDtl.amendAmt"
				#}
				ENDIF
				IF(CUST.DATA.menuId == "ODCM")THEN
				#{
					CUST.DATA.srvFieldNames = "dcOutCritDtl.dcFuncCode|dcOutCritDtl.dcRefNum|dcOutGenDtl.dcPordDtl.purOrdrDtl.purOrdHdrDtl.entityB2kId|dcOutCritDtl.dcReg.type|dcOutGenDtl.dcPordDtl.purOrdrDtl.purOrdHdrDtl.entityType"
					CUST.DATA.srvFieldNames = CUST.DATA.srvFieldNames + "|" + "dcChargeDtl.eventSrlNum|dcOutGenDtl.dcAmtDtl.dcOpenValue|dcOutCritDtl.dcCrncyCode"
					CUST.DATA.srvFieldNames = CUST.DATA.srvFieldNames + "|" + "dcAmendDtl.amendInd|dcAmendDtl.amendAmt"
				#}
				ENDIF
				IF(CUST.DATA.menuId == "IGM")THEN
				#{
					CUST.DATA.srvFieldNames = "bgInMsg.bgCritDtl.bgFuncCode|bgInMsg.bgCritDtl.bgSrlNum|bgInMsg.bgCommonDtl.bgB2kId|bgInMsg.bgCritDtl.bgType.type|bgInMsg.bgChargeDtl.entityId.entityType"
					CUST.DATA.srvFieldNames = CUST.DATA.srvFieldNames + "|" + "bgInMsg.bgChargeDtl.eventSrlNum|bgInMsg.bgCommonDtl.bgEventAmt|bgInMsg.bgCritDtl.bgCritCrncy"
					CUST.DATA.srvFieldNames = CUST.DATA.srvFieldNames + "|" + "bgInMsg.bgAmendDtl.amendInd|bgInMsg.bgAmendDtl.amendAmt"
				#}
				ENDIF
				IF(CUST.DATA.menuId == "IDCM")THEN
				#{
					CUST.DATA.srvFieldNames = "dcInMsgDtl.dcOutCritDtl.dcFuncCode|dcInMsgDtl.dcOutCritDtl.dcRefNum|dcInMsgDtl.dcOutGenDtl.dcPordDtl.purOrdrDtl.purOrdHdrDtl.entityB2kId|dcInMsgDtl.dcOutCritDtl.dcReg.type|dcInMsgDtl.dcOutGenDtl.dcPordDtl.purOrdrDtl.purOrdHdrDtl.entityType"
					CUST.DATA.srvFieldNames = CUST.DATA.srvFieldNames + "|" + "dcInMsgDtl.dcChargeDtl.eventSrlNum|dcInMsgDtl.dcOutGenDtl.dcAmtDtl.dcOpenValue|dcInMsgDtl.dcOutCritDtl.dcCrncyCode"
					CUST.DATA.srvFieldNames = CUST.DATA.srvFieldNames + "|" + "dcInMsgDtl.dcAmendDtl.amendInd|dcInMsgDtl.dcAmendDtl.amendAmt"
				#}
				ENDIF
				CUST.DATA.srvFieldValues = "funcCode|b2kRefNo|entityB2kId|b2kType|b2kIdType|eventSrlNum|b2kEventAmt|b2kCcy|amendInd|amendAmt"
				sub_getFieldValuesfromSRVStructB("10","CUST","DATA",CUST.DATA.srvFieldNames,CUST.DATA.srvFieldValues)
				PRINT(CUST.DATA.funcCode)
				PRINT(CUST.DATA.b2kRefNo)
				PRINT(CUST.DATA.entityB2kId)
				PRINT(CUST.DATA.b2kType)
				PRINT(CUST.DATA.b2kIdType)
				PRINT(CUST.DATA.eventSrlNum)
				CUST.DATA.b2kEventAmt = CDOUBLE(CUST.DATA.b2kEventAmt)
				PRINT(CUST.DATA.b2kEventAmt)
				PRINT(CUST.DATA.b2kCcy)
				PRINT(CUST.DATA.amendInd)
				CUST.DATA.amendAmt = CDOUBLE(CUST.DATA.amendAmt)
				PRINT(CUST.DATA.amendAmt)
				IF(((CUST.DATA.menuId == "OGM") OR (CUST.DATA.menuId == "IGM")) AND (CUST.DATA.b2kIdType == "")) THEN
				#{
					CUST.DATA.b2kIdType = "BNKGR"
				#}
				ENDIF
				
				BANCS.INPARAM.BINDVARS = CUST.DATA.chkTxnSeq + "|" + CUST.DATA.entityB2kId + "|" + CUST.DATA.b2kIdType + "|" + BANCS.STDIN.contextBankId
				BANCS.INPARAM.BINDVARS = BANCS.INPARAM.BINDVARS + "|" + CUST.DATA.entityB2kId + "|" + CUST.DATA.b2kIdType + "|" + BANCS.STDIN.contextBankId
				PRINT(BANCS.INPARAM.BINDVARS)
				sv_a = "UPDATE CUSTOM.C_TFCT_TBL SET SEQ_TRAN_ID_SL = ?SVAR WHERE B2K_ID = ?SVAR AND B2KID_TYPE = ?SVAR AND BANK_ID = ?SVAR "
				sv_a = sv_a + "AND SRL_NUM = (SELECT MAX(SRL_NUM) FROM CUSTOM.C_TFCT_TBL WHERE B2K_ID = ?SVAR AND B2KID_TYPE = ?SVAR AND BANK_ID = ?SVAR)"
				PRINT(sv_a)
				sv_a = urhk_dbSQLWithBind(sv_a)
				IF(sv_a != 0) THEN
				#{
					CUST.DATA.errorMsg = "Transaction Details : Updation Failed in C_DTD_P_EXT."
					CUST.DATA.errorFlg = "Y"
					GOTO ENDOFSCRIPT
				#}
				ENDIF
			#}
			ENDIF
		#}
		ENDIF
		
	#}
	ENDIF
	ENDOFSCRIPT :
		IF(CUST.DATA.errorFlg == "Y")THEN
		#{
			PRINT(CUST.DATA.errorMsg)
			sv_a = urhk_SRV_SetErr(CUST.DATA.errorMsg)
			sv_u = urhk_setOrbOut("SuccessOrFailure|N")
			BANCS.OUTPUT.successOrFailure = "F"
		#}
		ENDIF
		sub_deleteRepClassB("CUST","DATA")
END-->
